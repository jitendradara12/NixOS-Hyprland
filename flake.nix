# ==================================================
#  KoolDots (2026)
#  Project URL: https://github.com/LinuxBeginnings
#  License: GNU GPLv3
#  SPDX-License-Identifier: GPL-3.0-or-later
# ==================================================
{
  description = "KoolDots -- NixOS-Hyprland";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    home-manager.url = "github:nix-community/home-manager/master";
    home-manager.inputs.nixpkgs.follows = "nixpkgs";
    nixvim = {
      url = "github:nix-community/nixvim/main";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    #  To build from source, not recommended
    # hyprland = {
    #   url = "git+https://github.com/hyprwm/Hyprland?submodules=1";
    #   inputs.nixpkgs.follows = "nixpkgs";
    # };

    alejandra.url = "github:kamadorueda/alejandra";

    # Replacement for SWWW - which is archived
    awww.url = "git+https://codeberg.org/LGFae/awww";

    antigravity-cli-repo = {
      url = "github:Hy4ri/antigravity-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    waybar = {
      url = "github:alexays/waybar";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    ags = {
      type = "github";
      owner = "aylur";
      repo = "ags";
      ref = "v1";
    };

    catppuccin = {
      url = "github:catppuccin/nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    quickshell = {
      url = "git+https://git.outfoxxed.me/outfoxxed/quickshell";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    hyprmod-src = {
      url = "github:BlueManCZ/hyprmod";
      flake = false;
    };
  };

  outputs =
    inputs@{
      self,
      nixpkgs,
      ags,
      alejandra,
      hyprmod-src,
      ...
    }:
    let
      system = "x86_64-linux";

      # Dynamically import local settings if they exist to keep upstream merges clean
      localSettings = if builtins.pathExists ./local.nix
                      then import ./local.nix
                      else { host = "jak-hl"; username = "dwilliams"; };

      host = localSettings.host;
      username = localSettings.username;

      pkgs = import nixpkgs {
        inherit system;
        config = {
          allowUnfree = true;
        };
      };
      waybarWeatherPkg = pkgs.callPackage ./pkgs/waybar-weather.nix { };
      hyprlandBindings = pkgs.callPackage ./pkgs/hyprland-python-bindings.nix { };
      hyprmodPkg = pkgs.callPackage ./pkgs/hyprmod.nix { 
        hyprmodSrc = hyprmod-src;
        inherit hyprlandBindings;
      };
    in
    {
      packages.${system} = {
        waybar-weather = waybarWeatherPkg;
        hyprmod = hyprmodPkg;
      };
      nixosConfigurations = {
        "${host}" = nixpkgs.lib.nixosSystem rec {
          specialArgs = {
            inherit system;
            inherit inputs;
            inherit username;
            inherit host;
            customPkgs = { inherit hyprmodPkg; };
          };
          modules = [
            ./hosts/${host}/config.nix
            # inputs.distro-grub-themes.nixosModules.${system}.default
            ./modules/overlays.nix # nixpkgs overlays (CMake policy fixes)
            ./modules/quickshell.nix # quickshell module
            ./modules/packages.nix # Software packages
            # Allow broken packages (temporary fix for broken CUDA in nixos-unstable)
            { nixpkgs.config.allowBroken = true; }
            ./modules/fonts.nix # Fonts packages
            ./modules/portals.nix # portal
            ./modules/theme.nix # Set dark theme
            ./modules/ly.nix # ly greater with matrix animation
            ./modules/nh.nix # nix helper
            inputs.catppuccin.nixosModules.catppuccin
            # Integrate Home Manager as a NixOS module
            inputs.home-manager.nixosModules.home-manager
            {
              home-manager.useGlobalPkgs = true;
              home-manager.useUserPackages = true;
              home-manager.backupFileExtension = "hm-bak";

              # Ensure HM modules can access flake inputs (e.g., inputs.nixvim)
              home-manager.extraSpecialArgs = {
                inherit
                  inputs
                  system
                  username
                  host
                  ;
              };

              home-manager.users.${username} = {
                home.username = username;
                home.homeDirectory = "/home/${username}";
                home.stateVersion = "24.05";

                # Import your copied HM modules, plus host-specific configurations if they exist
                imports = [
                  ./modules/home/default.nix
                ] ++ (if builtins.pathExists ./hosts/${host}/home.nix then [ ./hosts/${host}/home.nix ] else []);
              };
            }
          ];
        };
      };
      # Code formatter
      formatter.x86_64-linux = alejandra.packages.x86_64-linux.default;
    };
}
